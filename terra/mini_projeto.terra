#include "/home/terra/TerraNet_v0.1/terra/TerraNet.defs"

pktype usrMsg from radioMsg with
  var ubyte[4]d8;
  var ushort[4]d16;
  var ulong[2] d32;
end

var ushort myNodeId = getNodeId();
var ushort dadNodeId = -1;
var ulong jump = -1;
var ushort msgId = 0;
var ushort msgIdAck = 0;
var usrMsg msgRadio;
var ubyte stat;

//type = 0 ->CreateNetWork
//type = 1 ->MyTemp

// Create Network
if myNodeId == 11 then
  await 2s;
  dadNodeId = 1;
  jump = 1;
  msgRadio.source = myNodeId;
  msgRadio.target = BROADCAST;
  msgRadio.type = 0;
  msgRadio.d32[0] = jump + 1;
  emit SEND(msgRadio);
  await SEND_DONE;
else
  msgRadio = await RECEIVE(0);
  dadNodeId = msgRadio.source;
  jump = msgRadio.d32[0];
  msgRadio.d32[0] = jump + 1;
  msgRadio.source = myNodeId;
  msgRadio.target = BROADCAST;
  msgRadio.type = 0;
  emit SEND(msgRadio);
  await SEND_DONE;
end

if dadNodeId != -1 then
  emit LED0(ON);
end

par do
  loop do
    //Get Temp and put in queue
    await 3s;
    await (10*jump)ms;
    emit REQ_TEMP();
    msgRadio.d16[0] = await TEMP;
    msgRadio.d16[1] = myNodeId;
    msgRadio.d16[2] = msgId;
    msgRadio.d32[0] = jump;
    inc msgId;
    stat = qPut(msgRadio);
  end
with
  loop do
    //Receive and put in queue
    msgRadio = await RECEIVE(1);
    if msgRadio.d16[2] == 0 or msgRadio.d16[2] >= msgIdAck then
      stat = qPut(msgRadio);
      msgIdAck = msgRadio.d16[2];
    end
  end
with
  loop do
    //Send message
    await Q_READY;
    loop do
      var ubyte ack;
      stat = qGet(msgRadio);
      msgRadio.source = myNodeId;
      msgRadio.target = dadNodeId;
      msgRadio.type = 1;
      emit SEND_ACK(msgRadio);
      loop do
        ack = await SEND_DONE_ACK;
        if ack != 0 then
          break;
        end
        emit SEND_ACK(msgRadio);
      end
      if qSize() == 0 then
        break;
      end
    end
  end
end